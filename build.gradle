plugins {
    id 'org.springframework.boot' version '3.0.2'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'com.palantir.git-version' version '1.0.0'
}

group = 'icu.clemon'
version = gitVersion()

def lombok = "org.projectlombok:lombok"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    api 'org.apache.commons:commons-lang3:3.12.0'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    compileOnly lombok
    annotationProcessor lombok
    testCompileOnly lombok
    testAnnotationProcessor lombok
}

java {
//    withJavadocJar()
    withSourcesJar()
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

publishing {
    publications {
        Production(MavenPublication) {
            from components.java

            groupId = project.group
            artifactId = project.name
            version = project.version

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = project.name
                description = 'Common lib of Spring Web App'
                url = 'https://github.com/c1emon/jcommon'

                licenses {
                    license {
                        name = "MIT Licence"
                        url = "https://www.mit.edu/~amini/LICENSE.md"
                    }
                }

                developers {
                    developer {
                        id = "clemon"
                        name = "Clemon Chan"
                        email = "cjw7360chen@gmail.com"
                    }
                }

                scm {
                    connection = "https://github.com/c1emon/jcommon"
                    developerConnection = "git@github.com:c1emon/jcommon.git"
                    url = "https://github.com/c1emon/jcommon.git"
                }
            }
        }
    }
    repositories {
        maven {
            // https://central.sonatype.org/publish/publish-guide/#releasing-to-central
            url = project.version.endsWith("SNAPSHOT") ? "https://s01.oss.sonatype.org/content/repositories/snapshots/" : "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.Production
}